# Author: Steeveeo
# Updated: Jan 29, 2011 @ 12:17 AM PST (-8 GMT)

# Use and Purpose:
# This chip uses holoModel to create lagless spinning fans
# for helicopters, planes, steampunk airships, and anything in
# between. First, place all of your fans on your ship by welding
# them where you want them (don't bother axising, just weld).
# Then take an Advanced Entity Marker and hook in all of your
# fans. Wire up accordingly.
#
# Inputs work in the following manner:
# Fans [ARRAY] - Advanced Ent Marker holding all your fans
# SpeedAll - Base Speed for all fans (added to
#            FanSpeed[Index,number]).
#
# Outputs:
# Channel [STRING] - This is the name of the gTable that controls
#                    individual fan speed. I would do this over a
#                    normal wire, but arrays just don't work in
#                    that medium. Speed index == Fan index.

# Fair Use and Legality:
# This chip is provided as is and is designed to work only
# as instructed. This is provided under Creative Commons and
# may be modified and/or shared with anyone as long as proper
# credit is given to the original author (Steeveeo). Failure to
# give credit by plagiarising or removing this header is subject
# to administrative punishment.

@name HoloPropeller Controller
@inputs Fans:array SpeedAll
@outputs Channel:string
@persist FanSpeed:gtable SetupStep
@trigger 

runOnLast(1)

#Startup
if(first()|dupefinished()|changed(Fans:count()))
{
    #Remove existing holos
    holoDeleteAll()
    
    Channel = "HoloPropeller_" + entity():toString()
    FanSpeed = gTable(Channel,0)
    
    #Create Propellers
    SetupStep = 1
    timer("Fan Setup",100)
        
    timer("Rotate",25)
}

#Build all fans
if(clk("Fan Setup"))
{
    #Loop through and create replacement holos
    Fan = Fans[SetupStep,entity]
    
    #Find parent of the fan, if any, so we know what to
    #stick the holo on.
    if(Fan:parent())
    {
        Parent = Fan:parent()
    }
    else
    {
        Parent = Fan
    }
    
    holoCreate(SetupStep)
    holoModel(SetupStep,Fan:model())
    holoMaterial(SetupStep,Fan:getMaterial())
    holoColor(SetupStep,Fan:getColor())
    holoAng(SetupStep,Fan:angles())
    holoPos(SetupStep,Fan:pos())
    holoParent(SetupStep,Parent)
    holoShadow(SetupStep,1)
    Fan:setAlpha(0)
    
    #Next Pass
    SetupStep++
    if(SetupStep <= Fans:count())
    {
        timer("Fan Setup",100)
    }
}

#Shutdown
if(last())
{
    #Restore all prop fans
    for(Index = 1, Fans:count())
    {
        Fan = Fans[Index,entity]
        Fan:setAlpha(255)
    }
}

#Spin the fan
if(clk("Rotate"))
{
    for(Index = 1, Fans:count())
    {
        Speed = FanSpeed[Index,number]
        holoAng(Index,holoEntity(Index):toWorld(ang(0,0,Speed*SpeedAll)))
    }
    
    timer("Rotate",25)
}
